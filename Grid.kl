
// ===============================================
//alias Vec2_i CellCoord;
//alias Vec2_i GridSize;
alias Vec3_i CellCoord;
alias Vec3_i GridSize;

// ===============================================
/// Grid interface.
interface IGrid
{
	/// Get the coordinate of the cell that contains pos.
	CellCoord getCellCoord(Point pos);
	/// Returns true if there's a cell allocated in the given coordinate exists.
	Boolean isValidCellCoord(CellCoord coord);
	/// Get cell value.
	UInt32 getCellValue(CellCoord coord);
	/// Set cell value.
	setCellValue!(CellCoord coord, UInt32 newVal);
};

// ===============================================
/// Simple packed rectangular grid that implements IGrid.
object SimpleGrid : IGrid
{
	private UInt32 cells[];
	private GridSize gridSize;
	private Point origin;
};

function SimpleGrid(GridSize gridSize, Point origin)
{
	this.gridSize = gridSize;
	this.origin = origin;
	//UInt32 numCells = gridSize.x * gridSize.y;
	UInt32 numCells = gridSize.x * gridSize.y * gridSize.z;
	this.cells.resize(numCells);
	for(UInt32 i = 0; i < numCells; ++i)
	{
		this.cells[i] = UInt32Max;
	}
}

inline private function Index SimpleGrid.cellToIndex(CellCoord coord)
{
	//return coord.y * this.gridSize.x + coord.x;
	return
		coord.z * this.gridSize.x * this.gridSize.y +
		coord.y * this.gridSize.x +
		coord.x;
}

inline function CellCoord SimpleGrid.getCellCoord(Point pos)
{
	//return CellCoord(pos.x - this.origin.x, pos.y - this.origin.y);
	return CellCoord(pos.x - this.origin.x, pos.y - this.origin.y, pos.z - this.origin.z);
}

inline function Boolean SimpleGrid.isValidCellCoord(CellCoord coord)
{
	//return coord.x >= 0 && coord.y >= 0 && coord.x < this.gridSize.x && coord.y < this.gridSize.y;
	return coord.x >= 0 && coord.y >= 0 && coord.z >= 0 && coord.x < this.gridSize.x && coord.y < this.gridSize.y && coord.z < this.gridSize.z;
}

inline function UInt32 SimpleGrid.getCellValue(CellCoord coord)
{
	return this.cells[this.cellToIndex(coord)];
}

inline function SimpleGrid.setCellValue!(CellCoord coord, UInt32 newVal)
{
	this.cells[this.cellToIndex(coord)] = newVal;
}
